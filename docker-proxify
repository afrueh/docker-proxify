#!/usr/bin/env perl
#################################################################################
#
# Copyright (c) 2014 Genome Research Ltd.
# 
# Author: Joshua C. Randall <jcrandall@alum.mit.edu>
# 
# This program is free software: you can redistribute it and/or modify it under
# the terms of the GNU General Public License as published by the Free Software
# Foundation; either version 3 of the License, or (at your option) any later
# version.
# 
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. See the GNU General Public License for more
# details.
# 
# You should have received a copy of the GNU General Public License along with
# this program. If not, see <http://www.gnu.org/licenses/>.
# 
#################################################################################

use strict;
use warnings;

use IO::File;
use Getopt::Long;
use Pod::Usage;

my $RS_BIN = "redsocks";
my $RS_TMP_CONFIG_FILE = "/tmp/redsocks.conf";
my $RS_SOCKS4_PORT = 12344;
my $RS_SOCKS5_PORT = 12345;
my $RS_CONNECT_PORT = 12346;
my $RS_HTTP_PORT = 12347;
my $RS_BIND_ADDR = "0.0.0.0";

my $IPTABLES_BIN = "iptables -w";
my @IPTABLES_SKIP_ADDRESS_SPECS = (
    "0.0.0.0/8",
    "10.0.0.0/8",
    "127.0.0.0/8",
    "169.254.0.0/16",
    "172.16.0.0/12",
    "192.168.0.0/16",
    "224.0.0.0/4",
    "240.0.0.0/4",
    );

my $http_proxy_url = "";
my $connect_proxy_url = "";
my $socks4_proxy_url = "";
my $socks5_proxy_url = "";

# rs_log is passed along to redsocks and can be one of: 
#   stderr
#   "file:/path/to/file"
#   syslog:FACILITY  facility is any of "daemon", "local0"..."local7"
my $rs_log = "stderr";

my $man = 0;
my $help = 0;

my $result = GetOptions( 
    "http_proxy:s" => \$http_proxy_url,
    "connect_proxy|https_proxy:s" => \$connect_proxy_url,
    "socks4_proxy:s" => \$socks4_proxy_url,
    "socks5_proxy:s" => \$socks5_proxy_url,
    "rs_log=s" => \$rs_log,
    "help|?" => \$help,
    "man" => \$man,
    ) or pod2usage(2);

pod2usage(-exitval => 0) if $help;
pod2usage(-exitval => 0, -verbose => 2) if $man;

=head1 NAME

docker-proxify - a wrapper for redsocks and iptables to setup one docker 
                 container to act as a transparent proxy for another.

=head1 SYNOPSIS

docker-proxify [options]

 Options:
    --http_proxy	set the http relay proxy to use (URL)
    --connect_proxy	set the http CONNECT proxy to use (URL)
    --https_proxy	synonym for --connect_proxy
    --socks4_proxy	set the SOCKS4 proxy to use (URL)
    --socks5_proxy	set the SOCKS5 proxy to use (URL)
    --help|-?		brief help message
    --man		full documentation

=head1 OPTIONS

=over 8

=item B<--help>

Print a brief help message and exit.

=item B<--man>

Prints the manual page and exits.

=back

=head1 DESCRIPTION

This program sets up configuration for redsocks, launches redsocks, and 
configures iptables rules so that any connections passed into this 
container will be routed to the specified proxy server. Only TCP is
supported at the moment. 

=cut

if ( ($http_proxy_url eq "") && ($connect_proxy_url eq "") && 
     ($socks4_proxy_url eq "") && ($socks5_proxy_url eq "") ) {
    pod2usage("Nothing to do, no proxies specified\n");
}

sub parse_proxy_url {
    my $proxy_url = shift;
    $proxy_url = lc($proxy_url);

    my $host = "";
    my $port = "";
    if( $proxy_url =~ m/^([a-z]+\:\/\/){0,1}([a-z0-9.-]+)\:([0-9]+)/ ) {
	$host = $2;
	$port = $3;
    } elsif( $proxy_url ne "" ) {
	print STDERR "Malformed proxy URL: $proxy_url\n";
    }
    return($host, $port);
}

my ($http_proxy_host, $http_proxy_port) = parse_proxy_url($http_proxy_url);
my ($connect_proxy_host, $connect_proxy_port) = parse_proxy_url($connect_proxy_url);
my ($socks4_proxy_host, $socks4_proxy_port) = parse_proxy_url($socks4_proxy_url);
my ($socks5_proxy_host, $socks5_proxy_port) = parse_proxy_url($socks5_proxy_url);

# Generate redsocks configuration file
my $conf_fh = new IO::File;
$conf_fh->open(">$RS_TMP_CONFIG_FILE") or die "Could not open $RS_TMP_CONFIG_FILE for output\n";

# First write base section
print $conf_fh <<EOF;
base {
        log_debug = on;
        log_info = on;
        log = "$rs_log";
        daemon = off;
        redirector = iptables;
}
EOF

sub generate_redsocks_section {
    my $host = shift;
    my $port = shift;
    my $local_port = shift;
    my $type = shift;

    my $rs_section = "";
    if( $host ne "" && $port > 0 && $port < 65536) {
	$rs_section = <<EOF;
redsocks {
    local_ip = $RS_BIND_ADDR;
    local_port = $local_port;
    ip = $host;
    port = $port;
    type = $type;
}

EOF
    }
    return $rs_section;
}

# now write sections for each proxy type
my $http_section = generate_redsocks_section($http_proxy_host, $http_proxy_port, $RS_HTTP_PORT, "http-relay");
print $conf_fh "$http_section\n";

my $connect_section = generate_redsocks_section($connect_proxy_host, $connect_proxy_port, $RS_CONNECT_PORT, "http-connect");
print $conf_fh "$connect_section\n";

my $socks4_section = generate_redsocks_section($socks4_proxy_host, $socks4_proxy_port, $RS_SOCKS4_PORT, "socks4");
print $conf_fh "$socks4_section\n";

my $socks5_section = generate_redsocks_section($socks5_proxy_host, $socks5_proxy_port, $RS_SOCKS5_PORT, "socks5");
print $conf_fh "$socks5_section\n";

# finished writing redsocks conf file
$conf_fh->close() or die "Could not close redsocks configuration file $RS_TMP_CONFIG_FILE\n";


# configure iptables chains for each type of config
sub configure_iptables_nat_chain {
    my $chain_type = shift;
    my $redsocks_port = shift;
    my $skip_address_specs = shift;
    
    my $out;
    $out = `${IPTABLES_BIN} -t nat -N REDSOCKS_${chain_type} 2>&1`;
    die "Could not create chain for REDSOCKS_${chain_type}\n".$out unless ($? == 0);
    
    foreach my $skip_address_spec (@{$skip_address_specs}) {
	$out = `${IPTABLES_BIN} -t nat -A REDSOCKS_${chain_type} -d ${skip_address_spec} -j RETURN 2>&1`;
	die "Could not add address spec skip rule for ${skip_address_spec}\n".$out unless ($? == 0);
    }

    `${IPTABLES_BIN} -t nat -A REDSOCKS_${chain_type} -p tcp -j REDIRECT --to-ports ${redsocks_port} 2>&1`;
    die "Could not setup redirect to redsocks port ${redsocks_port}\n".$out unless ($? == 0);
}

sub delete_iptables_nat_chain {
    my $chain_type = shift;
    
    my $out = `${IPTABLES_BIN} -t nat -F REDSOCKS_${chain_type} 2>&1`;	    
    die "Could not flush chain for REDSOCKS_${chain_type}\n".$out unless ($? == 0);

    $out = `${IPTABLES_BIN} -t nat -X REDSOCKS_${chain_type} 2>&1`;	    
    die "Could not delete chain for REDSOCKS_${chain_type}\n".$out unless ($? == 0);
}

if($connect_section ne "") {
    configure_iptables_nat_chain("CONNECT", $RS_CONNECT_PORT, \@IPTABLES_SKIP_ADDRESS_SPECS);
}

if($http_section ne "") {
    configure_iptables_nat_chain("HTTP", $RS_HTTP_PORT, \@IPTABLES_SKIP_ADDRESS_SPECS);
}

if($socks4_section ne "") {
    configure_iptables_nat_chain("SOCKS4", $RS_SOCKS4_PORT, \@IPTABLES_SKIP_ADDRESS_SPECS);
}

if($socks5_section ne "") {
    configure_iptables_nat_chain("SOCKS5", $RS_SOCKS5_PORT, \@IPTABLES_SKIP_ADDRESS_SPECS);
}

sub configure_iptables_dest_port {
    my $dest_addr_spec = shift;
    my $dest_port = shift;
    my $proxy_type = shift;
    my $chain = shift; # only PREROUTING or OUTPUT can be used with -i
    my $interface = shift || "";
    
    my $iptables_cmd = "${IPTABLES_BIN} -t nat -A $chain";
    if($interface ne "") {
	$iptables_cmd .= " -i $interface";
    }
    $iptables_cmd .= " -p tcp -d ${dest_addr_spec} --dport ${dest_port} -j REDSOCKS_${proxy_type}";
    my $out = `${iptables_cmd} 2>&1`;
    die "Could not setup proxy chain rule for ${dest_addr_spec}:${dest_port} to ${proxy_type}\n" unless ($? == 0);
}

sub delete_iptables_dest_port {
    my $dest_addr_spec = shift;
    my $dest_port = shift;
    my $proxy_type = shift;
    my $chain = shift; # only PREROUTING or OUTPUT can be used with -i
    my $interface = shift || "";

    my $iptables_cmd = "${IPTABLES_BIN} -t nat -D $chain";
    if($interface ne "") {
	$iptables_cmd .= " -i $interface";
    }
    $iptables_cmd .= " -p tcp -d ${dest_addr_spec} --dport ${dest_port} -j REDSOCKS_${proxy_type}";
    my $out = `${iptables_cmd} 2>&1`;
    die "Could not delete proxy chain rule for ${dest_addr_spec}:${dest_port} to ${proxy_type}\n" unless ($? == 0);
}

configure_iptables_dest_port("0.0.0.0/0", 80, "HTTP", "OUTPUT");
configure_iptables_dest_port("0.0.0.0/0", 443, "CONNECT", "OUTPUT");

configure_iptables_dest_port("0.0.0.0/0", 80, "HTTP", "PREROUTING", "docker0");
configure_iptables_dest_port("0.0.0.0/0", 443, "CONNECT", "PREROUTING", "docker0");


# fork and run redsocks until interrupt
# this code adapted from "safe pipe open for reading" 
# described in: http://perldoc.perl.org/perlipc.html#Safe-Pipe-Opens
my $child_stdout_fh;
my $pid = open($child_stdout_fh, "-|");
defined($pid) or die("can't fork: $!");

if (!$pid) { # $pid is only set in parent
    # child, first duplicate stderr to stdout
    open(STDERR, ">&STDOUT") or die "can't dup stdout to stderr\n";
    # call exec and never return (exec_lfs will exit on error)
    exec { $RS_BIN } $RS_BIN, ("-c", $RS_TMP_CONFIG_FILE) or die "Could not exec $RS_BIN\n";
    #`${RS_BIN} -c ${RS_TMP_CONFIG_FILE} 2>&1`;
    
    # as a safety precaution, force child to exit (this should never be reached)
    die "child process attempted to continue unexpectedly after exec\n";
} else { 
    # parent, process data from child
    sub cleanup {
	delete_iptables_dest_port("0.0.0.0/0", 80, "HTTP", "OUTPUT");
	delete_iptables_dest_port("0.0.0.0/0", 443, "CONNECT", "OUTPUT");
	
	delete_iptables_dest_port("0.0.0.0/0", 80, "HTTP", "PREROUTING", "docker0");
	delete_iptables_dest_port("0.0.0.0/0", 443, "CONNECT", "PREROUTING", "docker0");
	
	if($connect_section ne "") {
	    delete_iptables_nat_chain("CONNECT");
	}
	
	if($http_section ne "") {
	    delete_iptables_nat_chain("HTTP");
	}
	
	if($socks4_section ne "") {
	    delete_iptables_nat_chain("SOCKS4");
	}
	
	if($socks5_section ne "") {
	    delete_iptables_nat_chain("SOCKS5");
	}
	
	print "docker-proxify: cleaned up iptables after interrupt.\n";
	
	# send signal to kill redsocks in child
	kill 'TERM', $pid or die "could not terminate redsocks\n";
	
	# continuing, will exit after flusing output from child
    }

    $SIG{"INT"} = "cleanup";
    while(my $outline = <$child_stdout_fh>) {
	chomp $outline;
	print "redsocks: $outline\n";
    }
    
    die "docker-proxify: redsocks terminated.\n";
}
